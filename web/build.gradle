plugins {
    id "org.springframework.boot" version "2.2.4.RELEASE"
    id "io.spring.dependency-management" version "1.0.9.RELEASE"
    id "war"
}
ext {
    jjwtVersion = '0.9.1'
    restAssured = '4.2.0'
    jacksonDataBind = '2.10.2'
}
springBoot {
    mainClassName = "com.epam.esm.CertificateApplication"
}

sourceSets {
    integration {
        java.srcDir "$projectDir/src/integration/java"
        resources.srcDir "$projectDir/src/integration/resources"
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    integrationImplementation.extendsFrom testImplementation
    integrationRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integration.output.classesDirs
    classpath = sourceSets.integration.runtimeClasspath
}

dependencies {
    implementation project(':service')
    implementation project(':repository')
    implementation('org.springframework.boot:spring-boot-starter-web') {
        exclude module: 'spring-boot-starter-tomcat'
    }
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-jetty'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-security'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
    implementation group: 'junit', name: 'junit', version: '4.13'
    implementation group: 'io.jsonwebtoken', name: 'jjwt', version: "${jjwtVersion}"
    implementation group: 'io.rest-assured', name: 'rest-assured', version: "${restAssured}"
    implementation group: 'io.rest-assured', name: 'json-path', version: "${restAssured}"
    implementation group: 'io.rest-assured', name: 'xml-path', version: "${restAssured}"
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: "${jacksonDataBind}"
}